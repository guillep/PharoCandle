PCCompiledMethod
	superclass: #PCByteArray;
	instanceSpecification: #(#compiledMethod);
	instanceVariables: #();
	classVariables: #();
	package: #'PharoCandle-Kernel-Methods'.

PCCompiledMethod >> frameSize
[
	"Answer the size of temporary frame needed to run the receiver."
	"NOTE:  Versions 2.7 and later use two sizes of contexts."
	(self header bitAnd: 16r20000) = 0
		ifTrue: [ ^ 16 ]
		ifFalse: [ ^ 56 ]
]

PCCompiledMethod >> header
[
	"Answer the method header word containing information about the form of this method (e.g., number of literals) and the context needed to run it."
	^ self objectAt: 1
]

PCCompiledMethod >> initialPC
[
	"Answer the program counter for my first bytecode."
	^ 4 * (self numLiterals + 1) + 1
]

PCCompiledMethod >> isCompiledMethod
[
	^ true
]

PCCompiledMethod >> numLiterals
[
	"Answer the number of literals used by the receiver."
	^ (self header bitShift: -9) bitAnd: 16rFF
]

PCCompiledMethod >> numTemps
[
	"Answer the number of temporary variables used by this method."
	^ (self header bitShift: -18) bitAnd: 16r3F
]

PCCompiledMethod >> objectAt: index
[
	"Primitive. Answer the method header (if index = 1) or a literal (if index > 1) from the receiver. Essential. See Object documentation whatIsAPrimitive."
	<primitive: 68>
	self primitiveFailed
]

PCCompiledMethod >> selector 
[
	^ self objectAt: self numLiterals
]

PCCompiledMethod >> primitive 
[	"Answer the primitive index associated with the receiver.	Zero indicates that this is not a primitive method.	We currently allow 10 bits of primitive index, but they are in two places	for  backward compatibility.  The time to unpack is negligible,	since the reconstituted full index is stored in the method cache."	| primBits |	primBits := self header bitAnd: 16r100001FF.		^ (primBits bitAnd: 16r1FF) + (primBits bitShift: -19)
]

PCCompiledMethod >> objectAt: index put: value
[
	"Primitive. Store the value argument into a literal in the receiver. An index of 2 corresponds to the first literal. Fails if the index is less than 2 or greater than the number of literals. Answer the value as the result. Normally only the compiler sends this message because only the compiler stores values in CompiledMethods. Essential. See Object documentation whatIsAPrimitive."
	<primitive: 69>
	self primitiveFailed
]

PCCompiledMethod class
	instanceVariables: #().

