PCClass
	superclass: #PCBehavior;
	instanceSpecification: #(#pointers #words );
	instanceVariables: #(#name #instVarNames #classVariables );
	classVariables: #();
	package: #'PharoCandle-Kernel-Classes'.

PCClass >> classVariables
[
	"Answer the dictionary of class variables that I share with my sole instance, or nil if I have none."
	^ classVariables
]

PCClass >> initFrom: aPharoClass methodDict: newMethodDict
[
	"Fill in my instance variables from the given Class using the given MethodDictionary."
	superclass := PCObject.	"corrected later"
	methodDict := newMethodDict.
	format := aPharoClass format.
	name := (aPharoClass name copyFrom: 2 to: aPharoClass name size) asSymbol.	"omit leading M"
	instVarNames := aPharoClass instVarNames.
	classVariables := aPharoClass classPool.
	instVarNames size = 0
		ifTrue: [ instVarNames := nil ].
		classVariables size = 0
		ifTrue: [ classVariables := nil ]
]

PCClass >> instVarNames
[
	"Answer an Array of the receiver's instance variable names."
	instVarNames ifNil: [ ^ #() ].
	^ instVarNames
]

PCClass >> isMeta
[
	^ false
]

PCClass >> name
[
	^ name
]

PCClass >> theNonMetaClass
[
	^ self
]

PCClass >> classSide
[
	^ self class
]

PCClass class
	instanceVariables: #().

