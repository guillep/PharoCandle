PCMagnitude
	superclass: #PCObject;
	instanceSpecification: #(#pointers #words );
	instanceVariables: #();
	classVariables: #();
	package: #'PharoCandle-Kernel-Numeric'.

PCMagnitude >> < aMagnitude
[
	"Answer whether the receiver is less than the argument."
	^ self subclassResponsibility
]

PCMagnitude >> <= aMagnitude
[
	"Answer whether the receiver is less than or equal to the argument."
	^ (self > aMagnitude) not
]

PCMagnitude >> = aMagnitude
[
	"Compare the receiver with the argument and answer with true if the 
	receiver is equal to the argument. Otherwise answer false."
	^ self subclassResponsibility
]

PCMagnitude >> > aMagnitude
[
	"Answer whether the receiver is greater than the argument."
	^ aMagnitude < self
]

PCMagnitude >> >= aMagnitude
[
	"Answer whether the receiver is greater than or equal to the argument."
	^ (self < aMagnitude) not
]

PCMagnitude >> between: min and: max
[
	"Answer whether the receiver is less than or equal to the argument, max, 
	and greater than or equal to the argument, min."
	^ self >= min and: [ self <= max ]
]

PCMagnitude >> hash
[
	"Hash must be redefined whenever = is redefined."
	^ self subclassResponsibility
]

PCMagnitude >> max: aMagnitude
[
	"Answer the receiver or the argument, whichever has the greater 
	magnitude."
	self > aMagnitude
		ifTrue: [ ^ self ]
		ifFalse: [ ^ aMagnitude ]
]

PCMagnitude >> min: aMagnitude
[
	"Answer the receiver or the argument, whichever has the lesser 
	magnitude."
	self < aMagnitude
		ifTrue: [ ^ self ]
		ifFalse: [ ^ aMagnitude ]
]

PCMagnitude class
	instanceVariables: #().

