PCMagnitude
	superclass: #ClassPCObject;
	instanceSpecification: #(#pointers #words );
	instanceVariables: #();
	classVariables: #();
	package: #'PharoCandle-Kernel-Numeric'.

Class PCMagnitude >> < aMagnitude
[
	"Answer whether the receiver is less than the argument."
	^ self subclassResponsibility
]

Class PCMagnitude >> <= aMagnitude
[
	"Answer whether the receiver is less than or equal to the argument."
	^ (self > aMagnitude) not
]

Class PCMagnitude >> = aMagnitude
[
	"Compare the receiver with the argument and answer with true if the 
	receiver is equal to the argument. Otherwise answer false."
	^ self subclassResponsibility
]

Class PCMagnitude >> > aMagnitude
[
	"Answer whether the receiver is greater than the argument."
	^ aMagnitude < self
]

Class PCMagnitude >> >= aMagnitude
[
	"Answer whether the receiver is greater than or equal to the argument."
	^ (self < aMagnitude) not
]

Class PCMagnitude >> between: min and: max
[
	"Answer whether the receiver is less than or equal to the argument, max, 
	and greater than or equal to the argument, min."
	^ self >= min and: [ self <= max ]
]

Class PCMagnitude >> hash
[
	"Hash must be redefined whenever = is redefined."
	^ self subclassResponsibility
]

Class PCMagnitude >> max: aMagnitude
[
	"Answer the receiver or the argument, whichever has the greater 
	magnitude."
	self > aMagnitude
		ifTrue: [ ^ self ]
		ifFalse: [ ^ aMagnitude ]
]

Class PCMagnitude >> min: aMagnitude
[
	"Answer the receiver or the argument, whichever has the lesser 
	magnitude."
	self < aMagnitude
		ifTrue: [ ^ self ]
		ifFalse: [ ^ aMagnitude ]
]

PCMagnitude class
	instanceVariables: #().

